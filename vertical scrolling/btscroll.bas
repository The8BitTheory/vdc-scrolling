#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC
###############################################
# THIS ALLOWS SCROLLING OF A MONOCHROME IMAGE #
# ON THE VDC-CHIP                             #
###############################################

10 FAST:GRAPHIC5

##################
# LOAD VDC-BASIC #
##################
20 PRINT "INSERT DISK":GETKEY I$
30 BLOAD"BTPIC",B0,P20480
40 BLOAD"VDCBASICAC6.BIN",B0:SYS DEC("AC6")



#############
# VARIABLES #
#############

#  R:ROW WIDTH
50 IR=64:R=IR
#  F:FAST MODE
60 F=0
#  RM:REVERSE MODE
65 RM=0



######################
# VDC REGISTER SETUP #
######################

70 RGO 25,27:REM BITMAP MODE
80 RGA 25,255-26:REM NO ATTRIBUTES
90 RGO 28,24:REM 64K VRAM
100 RGW 1,R:REM VISIBLE CHARACTERS
110 RGW 36,0

120 SLOW:GRAPHIC0


#   RENDER IMAGE UPSIDE DOWN
130 AD=20480-R+22464:VA=0
140 FOR L=1 TO 352
150  RTV AD,VA,R
160  AD=AD-R:VA=VA+R
170 NEXT

180 VR=0:DISP VR

190 PRINT CHR$(7)


200 DO

210  GET I$

#    SCROLL UP AND DOWN
220  IF ASC(I$)=17 THEN IF VR<=9280 THEN VR=VR+R:D=-1
230  IF ASC(I$)=145 THEN IF VR>R THEN VR=VR-R:D=-1

#    LEFT AND RIGHT - JUST FOR THE FUN OF IT
233  IF ASC(I$)=157 THEN VR=VR-1:D=-1
235  IF ASC(I$)=29 THEN VR=VR+1:D=-1

#    UPDATE VRAM START ADDRESS IF CHANGED
240  IF D THEN DISP VR:D=0

#    TOGGLE 1/2 MHZ
250  IF I$=" " THEN BEGIN
260   F=NOT F
270   IF F THEN FAST:ELSE SLOW
280  BEND

#    CHANGE SCROLLING SPEED
290  IF I$="+" THEN IF R<640 THEN R=R+IR:PRINT "R="R
300  IF I$="-" THEN IF R>IR THEN R=R-IR:PRINT "R="R

#    TOGGLE REVERSE MODE
305  IF I$="R" THEN BEGIN
306   RM=NOT RM
307   IF RM THEN RGO 24,26:ELSE RGA 24,255-26
308  BEND

310 LOOP









