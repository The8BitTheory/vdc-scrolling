#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC
#BMP-SCROLL-AGI
# THIS PROGRAM'S GOAL IS TO CONVERT AGI BITMAPS TO VDC FORMAT
#
# REQ:
#  INPUT FILES NEED TO BE 16 COLORS (4BPP)
#  PREREQ IS THAT NO COLOR CLASHES ARE PRESENT IN THE FILES
#   IF THERE ARE, RESULT WILL DIFFER FROM EXPECTATION :-)
#   THE BITMAP HAS TO BE UPSIDE DOWN TO BE CONVERTED CORRECTLY


10 GRAPHIC 0
# DEFINE FUNCTIONS
20 GOSUB 970
30 BS=FN W(45)
40 BE=FN W(4624)
# DB=DATA BEGIN
# DE=DATA END
50 DB=BE+1:DE=DB+8000

60 PRINT "BASIC FROM "BS" TO "BE"."
70 PRINT "LEAVES "FRE(0)" BYTES FREE."
80 DD=PEEK(186)

90 PRINT "INSERT DATADISK":GETKEY I$

100 BLOAD "VDCBASIC2.0AC6",B0,U(DD):SYS DEC("AC6")

110 AP=32000

# 2 SCANLINES PER CHAR, 154 SCANLINES (EACH VAL +1)
120 RGW 0,127:RGW 4,155:RGW 6,100:RGW 7,140:RGW 9,1
130 RGO 25,128:REM BITMAP MODE ON
140 RGO 28,2 4:REM 64K VRAM
150 RGW 36,0

160 VMF 0,0,48000: REM CLEAR VRAM
170 ATTR AP

180 CATALOG U(DD)
190 PRINT"ENTER FILENAME"
200 PRINT"INPUT FILE:";:INPUT PF$

205 VP=AP:VR=0

230  PRINT "LOADING "PF$" ... ";
240  BLOAD (PF$),B0,P(DB),U(DD)
250  PRINT "DONE"
260  H1=PEEK(DB):H2=PEEK(DB+1)
270  IF (CHR$(H1)+CHR$(H2)) = "BM" THEN GOSUB 320

280  SLOW
290  BANK 15
300  BSAVE ("OUT.VDC"),B0,U(DD),P(DB+IS) TO P(DB+IS+48000)



#300 GOSUB 800

310 END

##################
# BITMAP ROUTINE #
##################

# HEADER
320 PRINT "BITMAP"
330 IS=FN W(DB+2)
340 PRINT "BFSIZE"IS
350 OS=FN W4(DB+10)
360 PRINT "BFOFFBITS"OS

# INFORMATIONBLOCK
#1030 PRINT "BISIZE"FN W4(DB+14)
370 BW=FN W4(DB+18)
380 PRINT "BIWIDTH"BW
390 BH=FN W4(DB+22)
400 PRINT "BIHEIGHT"BH
410 BC=FN W(DB+28)
420 PRINT "BIBITCOUNT"BC
430 IF BC<=8 THEN PRINT " INDICED COLORS"
440 IF BC >4 THEN PRINT " COLOR DEPTH "BC" NOT SUPPORTED. THE END.":END
450 PRINT "BICOMPRESSION"FN W4(DB+30)
460 PRINT "BISIZEIMAGE"FN W4(DB+34)
#1090 PRINT "BIXPELSPERMETER"FN W4(DB+38)
#1100 PRINT "BIYPELSPERMETER"FN W4(DB+42)
470 CU=FN W4(DB+46)
480 PRINT "BICLRUSED"CU
490 PRINT "BICLRIMPORTANT"FN W4(DB+50)

# PALETTE
500 IF CU<>0 THEN PRINT " PALETTE WITH "CU" ENTRIES"
510 IF BC<=8 THEN PRINT " PALETTE WITH "BC" ENTRIES": ELSE PRINT " NO PALETTE"




530 FAST:BANK0:ID=DB+OS
#540 VP=AP+N*8:VR=N*8:W=BW/2
#540 VP=40000:VR=24000
545 W=BW/2
550 PRINT "SCREEN-RAM:"VR", ATTRIBUTE-RAM:"VP
555 PRINT "Y-MAX:"(BH/2)",X-MAX:"(W-1)
560 S=TI:FOR Y=1 TO BH/2
570  FOR X=0 TO W-1


580   I1$=HEX$(PEEK(ID)):I2$=HEX$(PEEK(ID+W)):ID=ID+1
590   P1$=MID$(I1$,3,1):P2$=RIGHT$(I1$,1)
600   P3$=MID$(I2$,3,1):P4$=RIGHT$(I2$,1)
610   C1$=P1$:C2$=C1$

620   P3=P3$<>P1$
630   P4=P4$<>P1$

640   IF P2$<>P1$ THEN VMW VR,15:C2$=P2$
650   IF P3 AND P4 THEN VMW VR+W,255:C2$=P3$:GOTO 680
660   IF P3 THEN VMW VR+W,240:C2$=P3$:GOTO 680
670   IF P4 THEN VMW VR+W,15:C2$=P4$

680   VMW VP,DEC(C1$+C2$)
685   VP=VP+1
690   VR=VR+1
700  NEXT
710  ID=ID+W

720  VR=VR+160
#725  VP=AP+N*8+Y*80

730 NEXT
740 S=TI-S
750 PRINT S

760 VTR 0,DB+IS,48000

#760 VTR 0,DB+IS,24000
#760 VR=N*408

790 RETURN


800 DO
802  FOR X=0 TO 7

#     SOURCE ADDRESSES (*204 / +136)
805   A=24000+X*204
#     DESTINATION ADDRESSES (ALWAYS 0 / 16000)

#807   VMF 0,0,136:VMF 16136,0,68

#     COPY SCREEN-RAM: 272 BYTES
810   VMC A,0,4,34,80
#     COPY ATTRIBUTE RAM: 136 BYTES
815   VMC A+136,16000,4,17,80

817   GETKEY I$
820  NEXT

830 LOOP

899 RETURN


970 DEF FN W(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
980 DEF FN W4(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256+PEEK(ZZ+2)*65536+PEEK(ZZ+3)*16777216
990 DEF FN V(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))
1000 DEF FN HN(ZZ)=DEC(MID$(HEX$(ZZ),3,1))
1010 DEF FN LN(ZZ)=DEC(RIGHT$(HEX$(ZZ),1))

1020 RETURN