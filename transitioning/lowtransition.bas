#RetroDevStudio.MetaData.BASIC:7169,BASIC V7.0 VDC
#########################
# VDC-LOWRES TRANSITION #
#########################

##
##
## TRANSITIONING FROM ONE IMAGE TO ANOTHER BY OVERWRITING LINES RANDOMLY
##
##

# MAKE SURE ANY PRINT OUTPUT GOES TO THE 40 CHAR SCREEN
10 GRAPHIC 0

20 DEF FN W(ZZ)=PEEK(ZZ)+PEEK(ZZ+1)*256
30 BS=FN W(45)
40 BE=FN W(4624)
#  DB:DATA BEGIN
#  DE:DATA END
50 DB=BE+1:DE=DB+8000

#  SOME INTERESTING NUMBERS
60 PRINT "BASIC FROM "BS" TO "BE"."
70 PRINT "LEAVES "FRE(0)" BYTES FREE FOR DATA."

##################
# LOAD VDC-BASIC #
##################

80 DD=PEEK(186)
90 PRINT "INSERT DATADISK":GETKEY I$
100 BLOAD "VDCBASICAC6.BIN",B0,U(DD):SYS DEC("AC6")

#   A(0):VISIBLE AREA - LOADED WITH IMAGE #1
#   A(1):IMAGE #2
#   A(2):IMAGE #1 - COPY OF A(0)
110 A(0)=16000:A(1)=24000:A(2)=32000


#############
# VDC SETUP #
#############

#   2 SCANLINES PER CHAR, 156 SCANLINES (EACH VAL +1)
120 RGW 0,127:RGW 4,155:RGW 6,100:RGW 7,140:RGW 9,1
130 RGO 25,128:REM BITMAP MODE ON
140 RGO 28,2 4:REM 64K VRAM
150 RGW 36,0  :REM MEMORY REFRESH TO MINIMUM

#   SETUP PIXELS AND ATTRIBUTE RAM
160 VMF 0,15,16000
170 FOR X=0 TO 1
180  VMF A(X),0,8000
190 NEXT


#   SET START ADDRESSES FOR SCREEN-RAM AND ATTRIBUTE-RAM
200 DISP 0
210 ATTR A(0)



#########################
# SETUP LIST OF NUMBERS #
#########################

220 Y$=""
230 FOR C=0 TO 99
240  Y$=Y$+CHR$(C)
250 NEXT



####################
# LOAD IMAGE FILES #
####################

260 PF$="VDCLORES2.DAT"
270 D=DB
280 A=A(0)
290 GOSUB 600

300 PF$="LANDSCAPE.VDC"
310 D=DB+8000
320 A=A(1)
330 GOSUB 600

#   CREATE COPY OF IMAGE #1
340 S=TI
350 VMC A(0),A(2),8000
360 S=TI-S
370 PRINT "VMC TOOK "S" JIFFIES"

#   SET INDEX OF SOURCE-IMAGE TO 1
380 X=1

#   COPY-DESTINATION ADDRESS IS ALWAYS
#   THE FIRST ATTRIBUTE-RAM AREA
390 CD=A(0)


#############################
# TRANSITION INITIALIZATION #
#############################

400 PRINT "PRESS KEY TO SHOW TRANSITION"
410 GETKEY I$
420 PRINT " TRANSITIONING"

#   BENEFIT FROM 2 MHZ MODE
430 FAST

#   USING X$ FOR Y$ ALLOWS TO RESET THE LIST FOR NEXT TRANSITION EASILY
440 X$=Y$
450 R=RND(-TI)

# COPY-SOURCE ADDRESS
460 CS=A(X)



########################
# TRANSITION EXECUTION #
########################

#   ITERATE THROUGH ALL ROWS OF IMAGE
470 FOR L=0 TO 99

#    INDEX OF NEXT ROW-NUMBER ENTRY
480  P=INT(RND(1)*LEN(X$))+1
490  P$=MID$(X$,P,1)

#    N:ATTRIBUTE-RAM OFFSET OF ROW
500  N=ASC(P$)*80

#    REMOVE ENTRY FROM LIST (SPECIAL HANDLING FOR INDICES 1 AND LAST)
510  IF P=1 THEN X$=MID$(X$,2):GOTO 540
520  IF P=LEN(X$) THEN X$=LEFT$(X$,P-1):GOTO 540
530  X$=LEFT$(X$,P-1)+MID$(X$,P+1)

#    COPY PIXEL-ROW FROM SOURCE TO DESTINATION INCLUDING OFFSET
540  VMC CS+N,CD+N,80

550 NEXT

#   CHANGE COPY-SOURCE TO OTHER IMAGE (FOR NEXT TRANSITION OPERATION)
560 X=X+1:IF X>2 THEN X=1

570 SLOW:GOTO 400

580 SLOW:BANK 15

590 END



###################
# LOAD IMAGE FILE #
###################

# PF$:FILENAME
# D:  DESTINATION ADDRESS
# DD: DRIVE-NUMBER
600 PRINT "LOADING "PF$" ... ";
610 BLOAD (PF$),B0,P(D),U(DD)
620 PRINT "DONE"

630 S=TI
640 RTV D,A,8000
650 S=TI-S
660 PRINT "RTV TOOK "S" JIFFIES"

670 RETURN








